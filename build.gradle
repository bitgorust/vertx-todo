plugins {
    id 'java'
}

version '1.0-SNAPSHOT'

targetCompatibility = 1.8
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'io.vertx:vertx-core:3.7.0'
    compile 'io.vertx:vertx-web:3.7.0'
    compile 'io.vertx:vertx-redis-client:3.7.0'

    compileOnly 'io.vertx:vertx-codegen:3.7.0'
    annotationProcessor 'io.vertx:vertx-codegen:3.7.0'

    testCompile 'io.vertx:vertx-unit:3.7.0'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task annotationProcessing(type: JavaCompile, group: 'build') {
    source = sourceSets.main.java
    classpath = configurations.compile + configurations.compileOnly
    destinationDir = project.file('src/main/generated')
    options.compilerArgs = [
            "-proc:only",
            "-processor", "io.vertx.codegen.CodeGenProcessor",
            "-Acodegen.output=${destinationDir.absolutePath}"
    ]
}

compileJava {
    targetCompatibility = 1.8
    sourceCompatibility = 1.8

    dependsOn annotationProcessing
}

sourceSets {
    main {
        java {
            srcDirs += 'src/main/generated'
        }
    }
}

jar {
    // by default fat jar
    archiveName = 'fun-ginz-todo-backend-fat.jar'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    manifest {
        attributes 'Main-Class': 'io.vertx.core.Launcher'
        attributes 'Main-Verticle': 'fun.ginz.todo.verticles.SingleApplicationVerticle'
    }
}